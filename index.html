<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
        <meta name="viewport" content="width=1024, user-scalable=no">

        <title>Object-Oriented Perl - Chicago.PM</title>

        <!-- Required stylesheet -->
        <link rel="stylesheet" href="deck.js/core/deck.core.css">

        <!-- Extension CSS files go here. Remove or add as needed. -->
        <link rel="stylesheet" media="screen" href="deck.js/extensions/goto/deck.goto.css">
        <link rel="stylesheet" media="screen" href="deck.js/extensions/menu/deck.menu.css">
        <link rel="stylesheet" media="screen" href="deck.js/extensions/navigation/deck.navigation.css">
        <link rel="stylesheet" media="screen" href="deck.js/extensions/status/deck.status.css">
        <link rel="stylesheet" media="screen" href="deck.js/extensions/scale/deck.scale.css">

        <!-- Style theme. More available in /themes/style/ or create your own. -->
        <link rel="stylesheet" media="screen" href="deck.js/themes/style/swiss.css">

        <!-- Transition theme. More available in /themes/transition/ or create your own. -->
        <link rel="stylesheet" media="screen" href="deck.js/themes/transition/horizontal-slide.css">

        <!-- Basic black and white print styles -->
        <link rel="stylesheet" media="print" href="deck.js/core/print.css">

        <!-- Required Modernizr file -->
        <script src="deck.js/modernizr.custom.js"></script>

        <style>
            .slide.reverse {
                background-color: #ccf;
            }
        </style>
    </head>
    <body>
        <div class="deck-container">

            <!-- Begin slides. Just make elements with a class of slide. -->

            <section class="slide reverse">
                <h1>Object-Oriented Perl</h1>
                <p>Core, Moo, and Moose</p>
                <p>Doug Bell - Bank of America</p>
            </section>

            <section class="slide">
                <h1>Objects</h1>
            </section>

            <section class="slide">
                <h1>Things<br/>that can<br/>Do Things</h1>
            </section>

            <section class="slide">
                <h1>Data + Behavior</h1>
            </section>

            <section class="slide">
                <h1>Data:<br/>Attributes</h1>
            </section>

            <section class="slide">
                <h1>Behavior:<br/>Methods</h1>
            </section>

            <section class="slide">
                <h1>Class:<br/>Object Blueprint</h1>
            </section>

            <section class="slide">
                <h1>Constructor: Create Objects From Class</h1>
            </section>

            <section class="slide">
                <h1>Inheritance:<br/>My Class +<br/>Your Class</h1>
            </section>

            <section class="slide reverse">
                <h1>Class Patterns</h1>
            </section>

            <section class="slide">
                <h1>Model<br/>View<br/>Controller</h1>
            </section>

            <section class="slide">
                <h1>Dependency Injection</h1>
            </section>

            <section class="slide">
                <h1>Extract<br/>Transform<br/>Load</h1>
            </section>

            <section class="slide">
                <h1>Extract: Get Data</h1>
            </section>

            <section class="slide">
                <h1>Transform: Modify Data</h1>
            </section>

            <section class="slide">
                <h1>Load: Store Data</h1>
            </section>

            <section class="slide">
                <h1>Financial Data</h1>
            </section>

            <section class="slide">
                <h1>Time Series:<br/>Date/Value Pairs</h1>
            </section>

            <section class="slide reverse">
                <h1>Objects in Perl</h1>
            </section>

            <section class="slide">
                <h1>Data: Reference (<code>\%data</code>)</h1>
            </section>

            <section class="slide">
                <h1>Behavior: <code>sub</code></h1>
            </section>

            <section class="slide">
                <h1>Class: <code>package</code></h1>
            </section>

            <section class="slide">
                <h1>Object:<br/><code>bless \%data, 'package'</code></h1>
            </section>

            <section class="slide">
                <h2>Classic Perl Object</h2>
                <pre><code class="language-perl">
package Duke;
sub new {
    my ( $class, %data ) = @_;
    return bless \%data, $class;
}
                </code></pre>
            </section>

            <section class="slide">
                <h2>Instantiate an Object</h2>
                <pre><code class="language-perl">
use Duke;
my $duke = Duke->new(
    foo => "bar",
    baz => "fuzz",
);
                </code></pre>
                <p>Dereference (<code>-&gt;</code>) a bareword: Class method</p>
                <p>Class methods get class as first argument</p>
            </section>

            <section class="slide">
                <h2>Just a Reference</h2>
                <pre><code class="language-perl">
say $duke->{foo}; # "bar"
say $duke->{baz}; # "fuzz"
                </code></pre>
                <p>Breaks Encapsulation!</p>
            </section>

            <section class="slide">
                <h2>Blessed</h2>
                <pre><code class="language-perl">
use Scalar::Util qw( blessed );
say ref $duke;      # "Duke=HASH(0xdeadbeef)"
say blessed $duke;  # "Duke"
                </code></pre>
                <p>Package name is attached to the reference</p>
            </section>

            <section class="slide">
                <h2>Attributes</h2>
                <pre><code class="language-perl">
package Duke;
sub new {
    my ( $class, %args ) = @_;
    return bless {
        src   => $args{src},            # Extract
        xform => $args{xform} || [],    # Transform (optional)
        dest  => $args{dest},           # Load
    }, $class;
}
                </code></pre>
            </section>

            <section class="slide">
                <h2>Methods</h2>
                <pre><code class="language-perl">
package Duke;
sub add_xform {
    my ( $self, $new_xform ) = @_;
    push @{ $self->{xform} }, $new_xform;
    return;
}
                </code></pre>
                <p>Methods get object (<code>$self</code>) as first argument</p>
                <p>Allow appending extra transform steps.</p>
            </section>

            <section class="slide">
                <h2>Accessors and Mutators</h2>
                <p>Methods to get/set attributes</p>
                <pre><code class="language-perl">
package Duke;
sub src {
    my ( $self ) = @_;
    return $self->{src};
}
sub xform {
    my ( $self ) = @_;
    return $self->{xform};
}
sub dest {
    my ( $self, $new_dest ) = @_;
    if ( $new_dest ) {
        $self->{dest} = $new_dest;
    }
    return $self->{dest};
}
                </code></pre>
                <p>Allow new destinations, but not new sources or transforms.</p>
            </section>

            <section class="slide">
                <h2>Calling Methods</h2>
                <pre><code class="language-perl">
my $duke = Duke->new(
    src => "NYSE",
    xform => "FindSpikes",
    dest => "MySQL",
);
say $duke->src;
$duke->dest( "Postgres" );
say $duke->dest;
                </code></pre>
                <p>Don't let the caller use the reference directly.</p>
            </section>

            <!-- companion classes -->
            <section class="slide reverse">
                <h2>Companion Classes</h2>
                <p>Objectify All The Things</p>
                <img src="img/all-the-things.png" />
                <p>Build an API</p>
            </section>

            <section class="slide">
                <h2>Time Series</h2>
                <p>Data model</p>
                <pre><code class="language-perl">
package Duke::TimeSeries;
sub new {
    my ( $class, %args ) = @_;
    return bless {
        name => $args{name},    # Descriptive name
        data => $args{data},    # Hashref of date => value pairs
    }, $class;
}
sub name {
    my ( $self ) = @_; # read only
    return $self->{name};
}
sub data {
    my ( $self, $date ) = @_; # read only
    return $self->{data}{$date};
}
                </code></pre>
                <p>Mutability allows hard-to-find bugs</p>
            </section>

            <section class="slide">
                <h2>Source</h2>
                <p>Extract Time Series</p>
                <pre><code class="language-perl">
package Duke::Source;
sub new {
    my ( $class, %args ) = @_;
    return bless \%args, $class;
}
sub read_data {
    my ( $self, $name ) = @_;
    my %data;
    # ...
    return Duke::TimeSeries->new(
        name => $name,
        data => \%data,
    );
}
                </code></pre>
                <p>Notice the patterns? We'll do better soon</p>
            </section>

            <section class="slide">
                <h2>Destination</h2>
                <p>Load Time Series</p>
                <pre><code class="language-perl">
package Duke::Destination;
sub new {
    my ( $class, %args ) = @_;
    return bless \%args, $class;
}
sub write_data {
    my ( $self, $time_series ) = @_;
    # ...
    return;
}
                </code></pre>
                <p>Notice the patterns? We'll do better soon</p>
            </section>

            <section class="slide">
                <h2>Transform</h2>
                <p>Get one time series. Return another.</p>
                <pre><code class="language-perl">
package Duke::Xform;
sub new {
    my ( $class, %args ) = @_;
    return bless \%args, $class;
}
sub xform_data {
    my ( $self, $in_data ) = @_;
    my %out_data;
    # ...
    return Duke::TimeSeries->new(
        name => $in_data->name,
        data => \%out_data,
    );
}
                </code></pre>
                <p>Notice the patterns? We'll do better soon</p>
            </section>

            <section class="slide">
                <h2>Running The Job</h2>
                <pre><code class="language-perl">
package Duke;
sub run {
    my ( $self, $name ) = @_;
    my $data = $self->src->read_data( $name );
    for my $xform ( @{ $self->xform } ) {
        $data = $xform->xform_data( $data );
    }
    $self->dest->write_data( $data );
    return;
}
                </code></pre>
            </section>

            <section class="slide">
                <h2>By Your Powers Combined: ETL</h2>
                <pre><code class="language-perl">
my $duke = Duke->new(
    src => Duke::Source->new(),
    xform => [
        Duke::Xform->new(),
        Duke::Xform->new(),
    ],
    dest => Duke::Destination->new(),
);
$duke->run( 'IBM' );
                </code></pre>
            </section>

            <!-- inheritance -->
            <section class="slide reverse">
                <h1>Inheritance</h1>
            </section>

            <section class="slide">
                <h1>Subclasses</h1>
            </section>

            <section class="slide">
                <h1><code>@ISA</code></h1>
            </section>

            <section class="slide">
                <h1>
                    <code>our @ISA = ( 'Class' )</code><br/>
                </h1>
            </section>

            <section class="slide">
                <h1>
                    <code>use base 'Class'</code><br/>
                </h1>
            </section>

            <section class="slide">
                <h1>
                    <code>use parent 'Class'</code>
                </h1>
            </section>

            <section class="slide">
                <h2>A Useful Source</h2>
                <p>Inherits from <code>Duke::Source</code></p>
                <pre><code class="language-perl">
package Duke::Source::DBI;
use parent 'Duke::Source';
# new() is inherited
sub read_data {
    my ( $self, $name ) = @_;

    if ( !$self->{_dbh} ) { # Lazy attribute
        $self->{_dbh} = DBI->connect( $self->{dsn}, $self->{db_user}, $self->{db_pass} );
    }

    my $data = $self->{_dbh}->selectall_hashref(
        'SELECT date,value FROM time_series WHERE name=?',
        'date', # the field used for keys
        {},
        [ $name ]
    );

    return Duke::TimeSeries->new(
        name => $name,
        data => $data,
    );
}
                </code></pre>
            </section>

            <section class="slide">
                <h2>Using Subclasses</h2>
                <p><code>Duke::Source::DBI</code> is a subclass of <code>Duke::Source</code></p>
                <pre><code class="language-perl">
use Duke::Source::DBI;
my $src = Duke::Source::DBI->new(
    dsn => '...',
    db_user => 'user',
    db_pass => '***',
);
say blessed $src;
say $src->isa( 'Duke::Source' ) ? 'Yes' : 'No';
say $src->DOES( 'Duke::Source' ) ? 'Yes' : 'No';
                </code></pre>
            </section>

            <section class="slide reverse">
                <h1>Plain Old Perl Objects:<br/>Not So Bad?</h1>
            </section>

            <!-- type checking -->
            <section class="slide reverse">
                <h1>Type Constraints</h1>
            </section>

            <section class="slide">
                <h2>Only You Can Prevent Runtime Errors</h2>
                <pre><code class="language-perl">
my $duke = Duke->new(
    src => Duke::Destination->new(), # Uhoh
    # no dest?
);
# ... 200 lines in 20 files omitted
$duke->run(); # BOOM!
                </code></pre>
            </section>

            <section class="slide">
                <h2>Type Checking</h2>
                <pre><code class="language-perl">
package Duke;
sub new {
    my ( $class, %args ) = @_;

    die "src is required and must be Duke::Source object"
        unless blessed( $args{src} ) && $args{src}->isa( 'Duke::Source' );
    die "dest is required and must be Duke::Destination object"
        unless blessed( $args{dest} ) && $args{dest}->isa( 'Duke::Destination' );
    if ( $args{xform} ) {
        die "xform must be arrayref of Duke::Xform objects"
            if ref $args{xform} ne 'ARRAY'
                || grep {
                    !blessed( $_ ) || !$_->isa( 'Duke::Xform' )
                } @{ $args{xform} }
                ;
    }

    return bless {
        src   => $args{src},            # Extract
        xform => $args{xform} || [],    # Transform (optional)
        dest  => $args{dest},           # Load
    }, $class;
}
                </code></pre>
            </section>

            <section class="slide">
                <h2>In Subclasses</h2>
                <p>Override a method with SUPER::</p>
                <pre><code class="language-perl">
package Duke::Source::DBI;
use parent 'Duke::Source';
sub new {
    my ( $class, %args ) = @_;
    die "dsn is required" unless $args{dsn};
    die "db_user is required" unless $args{db_user};
    die "db_pass is required" unless $args{db_pass};
    my $self = $class->SUPER::new( %args );
    return $self;
}
                </code></pre>
            </section>

            <section class="slide">
                <h1>Repeat</h1>
            </section>
            <section class="slide">
                <h1>Repeat</h1>
            </section>
            <section class="slide">
                <h1>Repeat</h1>
            </section>

            <section class="slide">
                <h1>There's More Than One Way To Do It</h1>
            </section>

            <section class="slide">
                <h1>There Must Be a Better Way...</h1>
            </section>

            <!-- Moo (Earl) -->
            <section class="slide reverse">
                <h1>Moo</h1>
            </section>

            <section class="slide">
                <h1>Modern OO</h1>
            </section>

            <section class="slide">
                <h1>Earl ETL</h1>
            </section>

            <section class="slide">
                <h2>Attributes</h2>
                <p><code>has( 'attr', %args )</code></p>
                <pre><code class="language-perl">
package Earl;
use Moo;
has src => (
    is => 'ro',
    required => 1,
    isa => sub {
        my ( $src ) = @_;
        die "src must be Earl::Source object"
            unless blessed $src && $src->isa( 'Earl::Source' );
    },
);
has dest => (
    is => 'rw',
    required => 1,
    isa => sub {
        my ( $dest ) = @_;
        die "dest must be Earl::Destination object"
            unless blessed $dest && $dest->isa( 'Earl::Destination' );
    },
);
has xform => (
    is => 'ro',
    default => sub { [] },
    isa => sub {
        my ( $xform ) = @_;
        die "xform must be arrayref of Earl::Xform objects"
            if ref $xform ne 'ARRAY'
                || grep {
                    !blessed( $_ ) || !$_->isa( 'Earl::Xform' )
                } @{ $xform }
                ;
    },
);
                </code></pre>
            </section>

            <section class="slide">
                <h2>Moo Automatically...</h2>
                <ul>
                    <li>Creates a constructor</li>
                    <li>Checks for required attributes</li>
                    <li>Runs the type checks</li>
                    <li>Sets defaults for optional attributes</li>
                    <li>Creates read-only accessors</li>
                    <li>Creates read-write mutators</li>
                </ul>
                <p>Reduced 40 lines to 15</p>
            </section>

            <section class="slide">
                <h2>Subclasses</h2>
                <pre><code class="language-perl">
package Earl::Source::DBI;
use Moo;
extends 'Earl::Source';
has dsn => (
    is => 'ro',
    required => 1,
);
has db_user => (
    is => 'ro',
    required => 1,
);
has db_pass => (
    is => 'ro',
);
                </code></pre>
                <p><code>extends()</code> adds to <code>@ISA</code></p>
            </section>

            <section class="slide">
                <h2>Lazy Attributes</h2>
                <pre><code class="language-perl">
package Earl::Source::DBI;
has _dbh => (
    is => 'ro',
    lazy => 1,
    default => sub {
        my ( $self ) = @_;
        return DBI->connect( $self->{dsn}, $self->{db_user}, $self->{db_pass} );
    },
);
sub read_data {
    my ( $self, $name ) = @_;

    my $data = $self->_dbh->selectall_hashref(
        'SELECT date,value FROM time_series WHERE name=?',
        'date', # the field used for keys
        {},
        [ $name ]
    );

    return Earl::TimeSeries->new(
        name => $name,
        data => $data,
    );
}
                </code></pre>
                <p>Builds the default when requested</p>
                <p>Readability: Describe the what, not the how</p>
            </section>

            <section class="slide reverse">
                <h1>Type::Tiny</h1>
            </section>

            <section class="slide">
                <h2>Better Type Constraints</h2>
                <pre><code class="language-perl">
package Earl;
use Moo;
use Types::Standard qw( :all );
has src => (
    is => 'ro',
    required => 1,
    isa => InstanceOf['Earl::Source'],
);
has dest => (
    is => 'rw',
    required => 1,
    isa => InstanceOf['Earl::Destination'],
);
has xform => (
    is => 'ro',
    default => sub { [] },
    isa => ArrayRef[ InstanceOf['Earl::Xform'] ],
);
                </code></pre>
                <p>Saved 12 lines of code</p>
                <p>Much more readable: What, not How</p>
            </section>

            <section class="slide reverse">
                <h1>Roles</h1>
            </section>

            <section class="slide">
                <h1>Roles > Inheritance</h2>
            </section>

            <section class="slide">
                <h1>Multiple Roles > Multiple Inheritance</h1>
            </section>

            <section class="slide">
                <h1>Roles as Interface</h1>
            </section>

            <section class="slide">
                <h2>Source Role</h2>
                <pre><code class="language-perl">
package Earl::Source;
use Moo::Role;
requires 'read_data';
                </code></pre>
                <p>Checks for a read_data method</p>
            </section>

            <!-- method modifiers -->
            <section class="slide">
                <h2>Method Modifiers</h2>
                <p>Run a method <code>before</code>, <code>after</code>, or <code>around</code>
                another method</p>
                <pre><code class="language-perl">
package Earl::Source;
use Moo::Role;
requires 'read_data';
before read_data => sub {
    my ( $self, $name ) = @_;
    die "name is required" unless $name;
};
                </code></pre>
            </section>

            <section class="slide">
                <h2>around() Modifier</h2>
                <pre><code class="language-perl">
package Earl::Xform;
use Moo::Role;
requires 'xform_data';
around xform_data => sub {
    my ( $orig, $self, $in_data ) = @_;
    die "input data must be Earl::TimeSeries"
        unless blessed $in_data && $in_data->isa( 'Earl::TimeSeries' );
    my $out_data = $self->$orig( $in_data );
    die "output data must be Earl::TimeSeries"
        unless blessed $out_data && $out_data->isa( 'Earl::TimeSeries' );
    return $out_data;
};
                </code></pre>
            </section>

            <!-- Moose -->

            <!-- Moops (Jester) -->

            <!-- Lvalue accessors -->
            <!-- Method chaining -->
            <!-- Inside-out objects -->
            <!-- mro -->
            <!-- Signatures -->

            <!-- End slides. -->

            <!-- Begin extension snippets. Add or remove as needed. -->

            <!-- deck.navigation snippet -->
            <div aria-role="navigation">
                <a href="#" class="deck-prev-link" title="Previous">&#8592;</a>
                <a href="#" class="deck-next-link" title="Next">&#8594;</a>
            </div>

            <!-- deck.status snippet -->
            <p class="deck-status" aria-role="status">
                <span class="deck-status-current"></span>
                /
                <span class="deck-status-total"></span>
            </p>

            <!-- deck.goto snippet -->
            <form action="." method="get" class="goto-form">
                <label for="goto-slide">Go to slide:</label>
                <input type="text" name="slidenum" id="goto-slide" list="goto-datalist">
                <datalist id="goto-datalist"></datalist>
                <input type="submit" value="Go">
            </form>

            <!-- End extension snippets. -->
        </div>

        <!-- Required JS files. -->
        <script src="deck.js/jquery.min.js"></script>
        <script src="deck.js/core/deck.core.js"></script>

        <!-- Extension JS files. Add or remove as needed. -->
        <script src="deck.js/extensions/menu/deck.menu.js"></script>
        <script src="deck.js/extensions/goto/deck.goto.js"></script>
        <script src="deck.js/extensions/status/deck.status.js"></script>
        <script src="deck.js/extensions/navigation/deck.navigation.js"></script>
        <script src="deck.js/extensions/scale/deck.scale.js"></script>

        <!-- Initialize the deck. You can put this in an external file if desired. -->
        <script>
            $(function() {
                $.deck('.slide');
            });
        </script>

    </body>
</html>
